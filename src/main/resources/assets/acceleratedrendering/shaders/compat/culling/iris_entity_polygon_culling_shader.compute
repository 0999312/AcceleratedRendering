#version 460 core

struct Vertex {
    float x;
    float y;
    float z;
    int color;
    float u0;
    float v0;
    int uv1;
    int uv2;
    uint normal;
    uint iris_data_0;
    uint iris_data_1;
    uint iris_data_2;
    uint iris_data_3;
    uint iris_data_4;
};

struct VaryingData {
    int offset;
    int sharing;
    int color;
    int light;
    int overlay;
};

struct SharingData {
    mat4 transform;
    mat3 normal;
    uint flags;
    int mesh;
    uint iris_entity_0;
    uint iris_entity_1;
};

struct Polygon {
    int vertex1;
    int vertex2;
    int vertex3;
};

layout(local_size_x = 128) in;

layout(binding=1, std430) readonly buffer Vertices {
    Vertex vertices[];
};

layout(binding=2, std430) readonly buffer Sharings {
    SharingData sharings[];
};

layout(binding=3, std430) readonly buffer Varyings {
    VaryingData varyings[];
};

layout(binding=5, std430) readonly buffer PolygonsIn {
    Polygon polygonsIn[];
};

layout(binding=6, std430) writeonly buffer PolygonsOut {
    Polygon polygonsOut[];
};

layout(binding=0, offset=0) uniform atomic_uint indexCounter;

layout(location=0) uniform mat4 viewMatrix;
layout(location=1) uniform uint polygonCount;

bool isNormalVisable(Vertex vertex);

void main() {
    uint index = gl_GlobalInvocationID.x;

    if (index >= polygonCount) {
        return;
    }

    if (index == 0) {
        atomicCounterExchange(indexCounter, 0u);
    }

    memoryBarrier();

    Polygon polygon = polygonsIn[index];
    VaryingData varyingData = varyings[index - varyings[polygon.vertex1].offset];
    SharingData sharingData = varyingData.sharing != -1 ? sharings[varyingData.sharing] : SharingData(mat4(1.0), mat3(1.0), 0u, -1, 0u, 0u);
    uint noCull = sharingData.flags & 0x1u;

    if (noCull > 0u
    || isNormalVisable(vertices[polygon.vertex1])
    || isNormalVisable(vertices[polygon.vertex2])
    || isNormalVisable(vertices[polygon.vertex3])
    ) {
        int indexOut = int(atomicCounterAdd(indexCounter, 3)) / 3;

        polygonsOut[indexOut].vertex1 = polygon.vertex1;
        polygonsOut[indexOut].vertex2 = polygon.vertex2;
        polygonsOut[indexOut].vertex3 = polygon.vertex3;
    }
}

bool isNormalVisable(Vertex vertex) {
    vec3 normal = unpackSnorm4x8(vertex.normal).xyz;
    vec4 posStart = vec4(vertex.x, vertex.y, vertex.z, 1.0);
    vec4 posEnd = posStart + vec4(normal, 0.0);

    posStart = viewMatrix * posStart;
    posEnd = viewMatrix * posEnd;

    normal = normalize((posEnd - posStart).xyz);
    vec3 ref = normalize(-posStart.xyz);
    float result = dot(ref, normal);

    return result >= 0;
}